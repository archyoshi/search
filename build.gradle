plugins {
  id 'java'
  id 'jacoco'
  id 'maven'
  id 'signing'
}

group 'com.qwazr'
archivesBaseName = "qwazr-search"
version '1.5.0-SNAPSHOT'

sourceCompatibility = 1.11
targetCompatibility = 1.11

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

javadoc {
  options.encoding = 'UTF-8'
}

repositories {
  mavenCentral()
  mavenLocal()
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
  implementation platform('com.qwazr:qwazr-bom:1.5.0-SNAPSHOT')
  testImplementation group: 'junit', name: 'junit', version: '4.12'
  testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
  implementation group: 'com.qwazr', name: 'qwazr-utils', version: '1.5.0-SNAPSHOT'
  implementation group: 'com.qwazr', name: 'qwazr-server', version: '1.5.0-SNAPSHOT'
  implementation group: 'com.qwazr', name: 'qwazr-cluster', version: '1.5.0-SNAPSHOT'
  implementation group: 'com.qwazr', name: 'qwazr-binder', version: '1.5.0-SNAPSHOT'
  implementation group: 'org.apache.lucene', name: 'lucene-core', version: '8.4.1'
  implementation group: 'org.apache.lucene', name: 'lucene-codecs', version: '8.4.1'
  implementation group: 'org.apache.lucene', name: 'lucene-spatial', version: '8.4.1'
  implementation group: 'org.apache.lucene', name: 'lucene-spatial3d', version: '8.4.1'
  implementation group: 'org.apache.lucene', name: 'lucene-backward-codecs', version: '8.4.1'
  implementation group: 'org.apache.lucene', name: 'lucene-analyzers-common', version: '8.4.1'
  implementation group: 'org.apache.lucene', name: 'lucene-analyzers-stempel', version: '8.4.1'
  implementation group: 'org.apache.lucene', name: 'lucene-queryparser', version: '8.4.1'
  implementation group: 'org.apache.lucene', name: 'lucene-facet', version: '8.4.1'
  implementation group: 'org.apache.lucene', name: 'lucene-highlighter', version: '8.4.1'
  implementation group: 'org.apache.lucene', name: 'lucene-join', version: '8.4.1'
  implementation group: 'org.apache.lucene', name: 'lucene-grouping', version: '8.4.1'
  compile 'it.unimi.dsi:fastutil:8.2.2'
}

tasks.withType(JavaCompile) {
  options.encoding = "UTF-8"
}

task javadocJar(type: Jar) {
  archiveClassifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  sign configurations.archives
}

jacoco {
  toolVersion = "0.8.5"
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: findProperty(ossrhUsername) ?: '', password: findProperty(ossrhPassword) ?: '')
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: findProperty(ossrhUsername) ?: '', password: findProperty(ossrhPassword) ?: '')
      }

      pom.project {
        name 'qwazr-search'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'A Search Engine microservice with indexing and search features based on Apache Lucene'
        url 'https://github.com/qwazr/search'

        scm {
          connection 'scm:git:https://github.com/qwazr/search.git'
          developerConnection 'scm:svn:https://github.com/qwazr/search.git'
          url 'https://github.com/qwazr/search.git'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'ekeller'
            name 'Emmanuel Keller'
            email 'contact@qwazr.com'
          }
        }
      }
    }
  }
}
